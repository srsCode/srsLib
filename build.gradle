plugins {
    id 'java-library'
    id 'maven-publish'
    id 'idea'
    id 'checkstyle'
    id 'net.neoforged.gradle.userdev' version '7.0.+'
}
if (file('../.etc/srscode-MC.gradle').exists()) { apply from:'../.etc/srscode-MC.gradle' }

java {withSourcesJar()}

java.toolchain.languageVersion = JavaLanguageVersion.of(17)

checkstyle {
    setConfig resources.text.fromUri('https://srscode.github.io/checkstyle/srsCode-NF-checkstyle-J11+.xml')
    setReportsDir layout.buildDirectory.get().dir('reports').dir('checkstyle').asFile
    setToolVersion '10.12.5'
}
tasks.withType(Checkstyle).configureEach {
    it.reports.xml.required = false
    it.reports.html.required = true
}

dependencies {implementation "net.neoforged:neoforge:${neo_version}"}

def commonManifest = manifest {
    attributes([
        'Implementation-Vendor'   : "srsCode",
        'Implementation-Title'    : "${project.name}",
        'Implementation-Version'  : "${project.version}",
        'Build-Jdk'               : "${System.properties['java.vendor']} ${System.properties['java.vm.version']}",
        'Build-Timestamp'         : java.time.OffsetDateTime.now() as String
    ])
}

tasks.withType(JavaCompile).configureEach {options.encoding = 'UTF-8'}

tasks.withType(Jar).configureEach {
    from 'LICENCE.txt'
    manifest.from commonManifest
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifact jar
            artifact sourcesJar

            version project.version
            groupId = project.group
            artifactId = project.archivesBaseName

            pom {
                name = project.name
                description = 'A common Minecraft mod library for srsCode mods.'
                url = 'https://github.com/srsCode/srsLib'
                licenses {
                    license {
                        name = 'MIT license'
                        url = 'https://mit-license.org/'
                        distribution = 'repo'
                    }
                }
                scm {
                    url = 'https://github.com/srsCode/srsLib'
                    connection = 'scm:git:https://github.com/srsCode/srsLib.git'
                    developerConnection = 'scm:git:git@github.com:srsCode/srsLib.git'
                }
                issueManagement {
                    system = 'GitHub Issues'
                    url = 'https://github.com/srsCode/srsLib/issues'
                }
            }
        }
    }
    repositories {
        maven {
            mavenLocal()
        }
    }
}
